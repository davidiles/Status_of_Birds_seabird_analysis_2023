
    model {
  
  # --------------------------------
  # Fixed effects and observation
  # --------------------------------
  
  # Colony-level intercepts
  for(k in 1:ncolony){
    C[k] ~ dnorm(log(10000),0.01)
  }

 # Colony-level slopes
  slope_med ~ dnorm(0,0.1)
  slope_sd ~ dunif(0,1)
  slope_tau <- pow(slope_sd,-2)

  for(k in 1:ncolony){
    slope[k] ~ dnorm(slope_med,slope_tau)
  }
  
  noise_prior_sd <- 0.5
  noise_prior_var <- noise_prior_sd * noise_prior_sd

  for (k in 1:ncolony){
  	
  	sdnoise[k] ~ dlnorm(log(0.2) - 0.5*noise_prior_var,1/noise_prior_var)
  	taunoise[k] <- pow(sdnoise[k],-2)
  }

  for (i in 1:ncounts) { 
    count[i] ~ dpois(mu[i])                                    # response
    mu[i] <-  exp(eta_tau[i])                                  # expected response
    eta_tau[i] ~ dnorm(eta[i],taunoise[colony[i]])              # overdispersion
    eta[i] <- C[colony[i]] + slope[colony[i]]*(year[i]-1)   # process model - colony intercept and year-smoother
  }
  
  
  # --------------------------------
  # Derived parameters: predictions of annual expected counts
  # --------------------------------
  
  for (i in 1:nyearspred){
    for(k in 1:ncolony){
      etapred[i,k] <- exp(C[k] + slope[k]*(i-1) + 0.5*pow(sdnoise[k],2))

    }
    
  } #i
}
    
    
